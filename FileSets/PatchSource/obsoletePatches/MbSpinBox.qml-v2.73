////// GuiMods — added DarkMode

import QtQuick 1.1
import Qt.labs.components.native 1.0
import com.victron.velib 1.0

MbItem {
	id: root
	cornerMark: !readOnly && !spinbox.enabled

////// GuiMods — DarkMode
	property VBusItem darkModeItem: VBusItem { bind: "com.victronenergy.settings/Settings/Gui/ColorScheme" }
	property bool darkMode: darkModeItem.valid && darkModeItem.value == 0

	property bool valid: vItem.value !== undefined
	property alias bind: vItem.bind
	property string description
	property double stepSize: 0.5
	property int numOfDecimals: 1
	property bool readOnly: !userHasWriteAccess
	property alias unit: unit.text
	property alias localValue: spinbox.value
	property alias value: vItem.value
	property alias item: vItem
	property alias min: spinbox.minimumValue
	property alias max: spinbox.maximumValue
	property bool hasDefault: vItem.def !== undefined
	property bool useVirtualKeyboard
	property variant spinboxToolbarHandler: editMode ? _spinboxToolbarHandler : navigationHandler
	default property alias values: container.data

	editMode: spinbox.enabled
	height: keyboard.y + keyboard.height + 1
	toolbarHandler: spinboxToolbarHandler

	ToolbarHandler {
		id: _spinboxToolbarHandler
		property string leftIcon: "icon-toolbar-cancel"
		property string rightIcon: hasDefault ? "" : "icon-toolbar-ok"
		property string rightText: hasDefault ? qsTr("Default") : ""

		function leftAction()
		{
			cancel()
		}

		function rightAction()
		{
			save()
		}
	}

	signal exitEditMode(bool changed)
	signal maxValueReached()
	signal minValueReached()

	VBusItem {
		id: vItem
		isSetting: true
	}

	Item {
		id: verticalCentered
		height: root.defaultHeight
		width: root.width

		MbTextDescription {
			id: name
			anchors {
				left: parent.left; leftMargin: style.marginDefault
				verticalCenter: parent.verticalCenter
			}

			isCurrentItem: root.ListView.isCurrentItem
			text: root.description
			opacity: valid ? style.opacityEnabled : style.opacityDisabled
		}

		Item {
			id: container
			width: childrenRect.width
			height: childrenRect.height
			anchors {
				right: graytag.left; rightMargin: style.marginDefault
				verticalCenter: parent.verticalCenter
			}
		}

		MbBackgroundRect {
			id: graytag
////// GuiMods — DarkMode
			color: !darkMode ? (!spinbox.enabled ? "#ddd": "#fff") : (!spinbox.enabled ? "#4b4b4b": "#747474")
			height: spinbox.height + 6
			width:  spinbox.width  + unit.width + 10
////// GuiMods — DarkMode
			border.color: !darkMode ? "#ddd" : "#4b4b4b"
			border.width: spinbox.enabled ? 1 : 0
			anchors {
				right: parent.right; rightMargin: style.marginDefault
				verticalCenter: parent.verticalCenter
			}
		}

		MbTextValue {
			id: unit
			item.invalidText: ""
			anchors {
				right: parent.right; rightMargin: style.marginDefault + 5
				verticalCenter: spinbox.verticalCenter
			}
		}

		SpinBox {
			id: spinbox

			color: style.color2
			font.pixelSize: name.font.pixelSize
			font.family: name.font.family
			font.bold: false
			minimumValue: vItem.min === undefined ? 0 : vItem.min
			maximumValue: vItem.max === undefined ? 100 : vItem.max
			stepSize: root.stepSize
			enabled: false
			greyed: valid
			numOfDecimals: root.numOfDecimals
			anchors {
				right: unit.left
				verticalCenter: parent.verticalCenter
			}

			/* note: these functions break binding hence the Binding item below */
			Keys.onRightPressed: { if (value === maximumValue) maxValueReached(); spinbox.up(event.isAutoRepeat); }
			Keys.onLeftPressed: { if (value === minimumValue) minValueReached(); spinbox.down(event.isAutoRepeat); }
			Keys.onUpPressed: { if (value === maximumValue) maxValueReached(); spinbox.up(event.isAutoRepeat); }
			Keys.onDownPressed: { if (value === minimumValue) minValueReached(); spinbox.down(event.isAutoRepeat); }

			/* Focus is removed to ignore keypresses */
			Keys.onSpacePressed: save(false)

			Keys.onReturnPressed: {
				if (hasDefault) {
					value = vItem.defaultValue
				} else {
					save(false)
				}
			}

			Keys.onEscapePressed: cancel()
		}

		MouseArea {
			anchors {
				fill: spinbox
				leftMargin: -20
				rightMargin: -20
			}

			onPressed: handleMouseClick(true)
		}
	}

	Keyboard {
		id: keyboard
		anchors.top: verticalCentered.bottom
		anchors.topMargin: -1
		width: root.width
		active: spinbox.enabled && useVirtualKeyboard
		layout: "KeyboardLoaderPlusMin.qml"
		onAnimatingChanged: listview.positionViewAtIndex(currentIndex, ListView.Contain)
	}

	/* binding is done explicitly to reenable binding after edit */
	Binding {
		target: spinbox
		property: "value"
		value: vItem.value
		when: valid && !spinbox.enabled
	}

	function edit(isMouse)
	{
		if (valid && !readOnly)
		{
			useVirtualKeyboard = isMouse === true
			spinbox.enabled = true
			spinbox.focus = true
		}
	}

	function save() {
		vItem.setValue(spinbox.value)
		focus = true;
		spinbox.enabled = false
		exitEditMode(true)
	}

	function cancel() {
		focus = true
		spinbox.enabled = false
		exitEditMode(false)
	}
}
